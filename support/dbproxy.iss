; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Dbproxy_Server"
#define MyAppVersion "1.0"
#define SetupExeName "Dbproxy_Server"
#define MinJRE "1.7"
;定义一些全局变量

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8F31921F-26FB-43B2-89AB-0B77415B0C3C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=true
AlwaysRestart=no
LicenseFile=inno\license.txt
;InfoBeforeFile=inno\info_before_install.txt
;InfoAfterFile=inno\info_after_install.txt
VersionInfoVersion=1.0
VersionInfoTextVersion=1.0
PrivilegesRequired=admin
ArchitecturesInstallIn64BitMode=x64
OutputDir=output
OutputBaseFilename={#SetupExeName}
Compression=lzma
SolidCompression=true
UninstallFilesDir ={app}\
UninstallDisplayName =UnInstallServer.exe
; Tell Windows Explorer to reload the environment
ChangesEnvironment=yes
;{pf} 程序文件位置。系统的  Program Files 目录的路径，一般来说是“C:\Program Files”
[Languages]
;Name: english; MessagesFile: compiler:Default.isl
Name: chinese; MessagesFile: compiler:Default_cn.isl

[Files]
Source: Application\server\*; DestDir: {app}\server; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: Application\service\*; DestDir: {app}\service; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: Application\sp\*; DestDir: {app}\sp; Flags: ignoreversion recursesubdirs createallsubdirs;

;Source: {win}\regsvr32.exe; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;{app} 用户在安装向导中的选择目标位置页中选定的应用程序目录
Source: compiler:psvince.dll; Flags: dontcopy noencryption
Source: inno\psvince.dll; DestDir: {app}\

[Types]
Name: "full"; Description: "全部安装"
Name: "custom"; Description: "自定义安装";  Flags: iscustom


[Components]
Name: "Id_Server"; Description: "Id生成服务"; Types: full
Name: "Dbproxy_Service"; Description: "数据库代理服务"; Types: full
Name: "Dbproxy_Sp"; Description: "数据库代理服务交换平台"; Types: full

[Registry]
Root: HKLM; Subkey: Software\Dbproxy_Server; ValueType: string; ValueName :Installed Version; ValueData:'1.0'; Flags: uninsdeletekey
Root: HKLM; Subkey: Software\Dbproxy_Server; ValueType: string; ValueName :Path; ValueData:{app}; Flags: uninsdeletekey

[Run]
Filename: "{app}\server\bin\InstallApp-NT.bat"; StatusMsg:"正在安装Id生成服务......"; WorkingDir:"{app}\server"; Flags: nowait runhidden; Components: Id_Server
Filename: "{app}\service\bin\InstallApp-NT.bat"; StatusMsg:"正在安装数据库代理服务......"; WorkingDir:"{app}\service"; Flags: nowait runhidden; Components: Dbproxy_Service
Filename: "{app}\sp\bin\InstallApp-NT.bat"; StatusMsg:"正在安装数据库代理服务交换平台......"; WorkingDir:"{app}\sp"; Flags: runhidden; Components: Dbproxy_Sp
Filename: "net.exe"; Parameters: "start Id_Server"; StatusMsg:"正在启动Id生成服务......"; Flags: nowait runhidden 
Filename: "net.exe"; Parameters: "start Dbproxy_Service"; StatusMsg:"正在启动数据库代理服务......"; Flags: nowait runhidden
Filename: "net.exe"; Parameters: "start Dbproxy_Sp"; StatusMsg:"正在启动数据库代理服务交换平台......"; Flags: nowait runhidden

[UninstallRun]
Filename: "{app}\server\bin\UninstallApp-NT.bat"; WorkingDir: "{app}\server"; Flags: runminimized runhidden
Filename: "{app}\service\bin\UninstallApp-NT.bat"; WorkingDir: "{app}\service"; Flags: runminimized runhidden
Filename: "{app}\sp\bin\UninstallApp-NT.bat"; WorkingDir: "{app}\sp"; Flags: runminimized runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}";


[Code]

//查找安装版本
function GetInstalledVersion():String;
var
  InstalledVersion: String;
begin
   InstalledVersion := '';
   RegQueryStringValue(HKLM, 'Software\Dbproxy_Server',
     'Installed Version', InstalledVersion);
    Result:=InstalledVersion;
end;

// 检查是否安装jdk7以上版本
function checkInstallJdk7: Boolean;
var
  JREVersion: string;
begin
  // read JRE version
  Result := RegQueryStringValue(HKLM32, 'Software\JavaSoft\Java Runtime Environment','CurrentVersion', JREVersion);
  // if the previous reading failed and we're on 64-bit Windows, try to read 
  // the JRE version from WOW node
  if not Result and IsWin64 then
    Result := RegQueryStringValue(HKLM64, 'Software\JavaSoft\Java Runtime Environment','CurrentVersion', JREVersion);
  // if the JRE version was read, check if it's at least the minimum one
  if Result then
    Result := CompareStr(JREVersion, '{#MinJRE}') >= 0;
end;

//初始化安装方法  execute first
function InitializeSetup():boolean;
var bResult:boolean;
var PreVer :String;
var javaInstalled : Boolean;
begin
    PreVer := GetInstalledVersion();
    Result:= true;
    if length(PreVer) > 0 then 
    begin
      MsgBox('此程序已安装，请卸载后再安装程序。安装程序将关闭。',mbError,MB_OK);
      Result:= false;
      Exit;
    end;  
    bResult :=  CheckForMutexes('Dbproxy_Server');
    if  bResult = true then
    begin
       MsgBox('另一安装程序已经在运行，此安装程序将退出。',mbInformation,MB_OK);
       Result := False;
       Exit;
    end;
    javaInstalled := checkInstallJdk7();
    if javaInstalled = true then
    begin
        CreateMutex('Dbproxy_Server');
    end else
    begin
	MsgBox('请安装jdk1.7或以上版本！', mbInformation, MB_OK);
	Result := False;
	Exit;
    end;
end;